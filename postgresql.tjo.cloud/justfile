export TF_CLI_ARGS := "-var-file=terraform.secrets.tfvars"

default:
  @just --list

apply:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu init
  tofu apply

create:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu init
  tofu apply --target 'module.proxmox_node'
  tofu apply --target 'technitium_record.for_node'
  tofu apply --target 'local_file.ansible_inventory'

destroy:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu destroy

destroy-only node:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu init
  tofu destroy --target 'module.proxmox_node["{{node}}"]'

get-users:
  cd {{source_directory()}}/terraform && tofu output -json | jq '.users.value'

get-administrators:
  cd {{source_directory()}}/terraform && tofu output -json | jq '.administrators.value'

configure node tag="":
  #!/usr/bin/env sh
  cd {{source_directory()}}/ansible
  export VERSION=$(git describe --tags --always --dirty)
  TAGS=""; if [ -n "{{tag}}" ]; then TAGS="--tags {{tag}}"; fi
  ansible-playbook \
    --extra-vars "@{{source_directory()}}/../global.yaml" \
    --extra-vars "tjo_cloud_version=${VERSION}" \
    --inventory inventory.yaml \
    $TAGS \
    playbook.yaml --limit {{node}}

configure-all tag="":
  #!/usr/bin/env sh
  cd {{source_directory()}}/ansible
  export VERSION=$(git describe --tags --always --dirty)
  TAGS=""; if [ -n "{{tag}}" ]; then TAGS="--tags {{tag}}"; fi
  ansible-playbook \
    --extra-vars "@{{source_directory()}}/../global.yaml" \
    --extra-vars "tjo_cloud_version=${VERSION}" \
    --inventory inventory.yaml \
    $TAGS \
    playbook.yaml

ssh node:
  ssh bine@{{node}}.postgresql.cloud.internal -p 2222
