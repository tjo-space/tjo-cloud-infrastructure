GATEWAY_CRDS_VERSION := "v1.1.0"
PROMETHEUS_CRDS_VERSION := "main"
ENVOY_CRDS_VERSION := "1.2.8"
ARGOCD_CRDS_VERSION := "2.14.10"
GRAFANA_ALLOY_CRDS_VERSION := "0.3.4"
SEALED_SECRETS_VERSION := "0.34.0"

default:
  @just --list

module-cluster-core-crds:
  @rm -rf modules/cluster-core/crds
  @mkdir -p modules/cluster-core/crds

  # Prometheus
  @curl -L -o modules/cluster-core/crds/podmonitors.yaml \
    "https://raw.githubusercontent.com/prometheus-community/helm-charts/{{PROMETHEUS_CRDS_VERSION}}/charts/kube-prometheus-stack/charts/crds/crds/crd-podmonitors.yaml"
  @curl -L -o modules/cluster-core/crds/servicemonitors.yaml \
    "https://raw.githubusercontent.com/prometheus-community/helm-charts/{{PROMETHEUS_CRDS_VERSION}}/charts/kube-prometheus-stack/charts/crds/crds/crd-servicemonitors.yaml"

  # Gateway API
  @curl -L -o modules/cluster-core/crds/gateway-api.yaml \
    "https://github.com/kubernetes-sigs/gateway-api/releases/download/{{GATEWAY_CRDS_VERSION}}/experimental-install.yaml"

  # Envoy Proxy
  @curl -L -o - \
    "https://github.com/envoyproxy/gateway/archive/refs/tags/v{{ENVOY_CRDS_VERSION}}.tar.gz" \
    | tar -xz -C modules/cluster-core/crds --strip-components=5 --wildcards "gateway-{{ENVOY_CRDS_VERSION}}/charts/gateway-helm/crds/generated/*.yaml"

  # Argo CD
  @curl -L -o - \
    "https://github.com/argoproj/argo-cd/archive/refs/tags/v{{ARGOCD_CRDS_VERSION}}.tar.gz" \
    | tar -xz -C modules/cluster-core/crds --strip-components=3 --wildcards "argo-cd-{{ARGOCD_CRDS_VERSION}}/manifests/crds/*.yaml"
  rm modules/cluster-core/crds/kustomization.yaml

  # Grafana Alloy
  @curl -L -o modules/cluster-core/crds/grafana-alloy.yaml \
    "https://github.com/grafana/alloy-operator/releases/download/alloy-operator-{{GRAFANA_ALLOY_CRDS_VERSION}}/collectors.grafana.com_alloy.yaml"

  # Sealed Secrets
  @curl -L -o modules/cluster-core/crds/sealed-secrets.yaml \
    "https://github.com/bitnami-labs/sealed-secrets/raw/refs/tags/v{{SEALED_SECRETS_VERSION}}/helm/sealed-secrets/crds/bitnami.com_sealedsecrets.yaml"

destroy:
  tofu state rm module.cluster-core || true
  tofu state rm module.cluster-components || true
  tofu destroy -target module.cluster

apply:
  tofu init
  tofu apply -target module.cluster
  tofu apply -target module.cluster-core.helm_release.talos-ccm
  tofu apply -target module.cluster-core.helm_release.proxmox-ccm
  tofu apply -target module.cluster-core.kubectl_manifest.crds
  tofu apply -target module.cluster-core.helm_release.cilium
  tofu apply -target module.cluster-core
  tofu apply

apply-full:
  tofu init
  tofu apply

remove node: etcd-snapshot
  #!/usr/bin/env bash
  IP=$(tofu output -json | jq -r '.nodes.value | map(select(.name == "{{node}}")) | first | .ipv4')

  talosctl -n ${IP} reset
  kubectl delete node "{{node}}
  echo "Node {{node}} removed from kubernetes. Now remove it from terraform configuration!"

etcd-snapshot prefix="manual":
  #!/usr/bin/env bash
  set -eou pipefail

  NODE=$(tofu output -json | jq -r '.nodes.value | map(select(.type == "controlplane")) | first | .ipv4')

  echo "= Taking etcd snapshot on node ${NODE}"
  talosctl -n ${NODE} etcd snapshot db.snapshot
  talosctl -n ${NODE} cp /var/lib/etcd/member/snap/db backup/etcd/snapshot-{{prefix}}-$(date +%F_%H-%M-%S)
  echo "= Snapshoting done!"

upgrade-talos version:
  #!/usr/bin/env bash
  set -eou pipefail

  echo "= Talos version: {{ version }}"

  just etcd-snapshot "pre-upgrade-talos-{{version}}"

  SCHEMATIC_ID=$(tofu output -json | jq -r '.talos.value.schematic_id')

  NODES=$(tofu output -json | jq -r '.nodes.value | sort_by(.type, .name)[] | .ipv4')
  for node in ${NODES}; do
    echo "== Upgrading ${node}"
    talosctl --nodes ${node} upgrade --wait \
      --image factory.talos.dev/nocloud-installer/${SCHEMATIC_ID}:{{ version }}
    sleep 10
    talosctl --nodes ${node} health
    sleep 2
  done

  just etcd-snapshot "post-upgrade-talos-{{version}}"

upgrade-kubernetes version:
  #!/usr/bin/env bash
  set -eou pipefail

  echo "= Kubernetes version: {{ version }}"

  just etcd-snapshot "pre-upgrade-kubernetes-{{version}}"

  NODES=$(tofu output -json | jq -r '.nodes.value | sort_by(.type, .name)[] | .ipv4')
  for node in ${NODES}; do
    echo "== Upgrading ${node}"
    talosctl --nodes ${node} upgrade-k8s --to {{ version }}
    sleep 10
    talosctl --nodes ${node} health
    sleep 2
  done

  just etcd-snapshot "post-upgrade-kubernetes-{{version}}"
