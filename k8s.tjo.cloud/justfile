GATEWAY_CRDS_VERSION := "v1.4.1"
PROMETHEUS_CRDS_VERSION := "main"
ENVOY_CRDS_VERSION := "1.6.1"
ARGOCD_CRDS_VERSION := "3.2.3"
GRAFANA_ALLOY_CRDS_VERSION := "0.3.15"
SEALED_SECRETS_CRDS_VERSION := "0.34.0"
K8UP_CRDS_VERSION := "2.13.1"
CERT_MANAGER_CRDS_VERSION := "1.19.2"

export TALOSCONFIG := "admin.talosconfig"
export KUBECONFIG := "admin.kubeconfig"

default:
  @just --list

module-cluster-core-crds:
  @rm -rf modules/cluster-core/crds
  @mkdir -p modules/cluster-core/crds

  # Prometheus
  @curl -L -o modules/cluster-core/crds/podmonitors.yaml \
    "https://raw.githubusercontent.com/prometheus-community/helm-charts/{{PROMETHEUS_CRDS_VERSION}}/charts/kube-prometheus-stack/charts/crds/crds/crd-podmonitors.yaml"
  @curl -L -o modules/cluster-core/crds/servicemonitors.yaml \
    "https://raw.githubusercontent.com/prometheus-community/helm-charts/{{PROMETHEUS_CRDS_VERSION}}/charts/kube-prometheus-stack/charts/crds/crds/crd-servicemonitors.yaml"

  # Gateway API
  @curl -L -o modules/cluster-core/crds/gateway-api.yaml \
    "https://github.com/kubernetes-sigs/gateway-api/releases/download/{{GATEWAY_CRDS_VERSION}}/experimental-install.yaml"

  # Envoy Proxy
  @curl -L -o - \
    "https://github.com/envoyproxy/gateway/archive/refs/tags/v{{ENVOY_CRDS_VERSION}}.tar.gz" \
    | tar -xz -C modules/cluster-core/crds --strip-components=5 --wildcards "gateway-{{ENVOY_CRDS_VERSION}}/charts/gateway-helm/crds/generated/*.yaml"

  # Argo CD
  @curl -L -o - \
    "https://github.com/argoproj/argo-cd/archive/refs/tags/v{{ARGOCD_CRDS_VERSION}}.tar.gz" \
    | tar -xz -C modules/cluster-core/crds --strip-components=3 --wildcards "argo-cd-{{ARGOCD_CRDS_VERSION}}/manifests/crds/*.yaml"
  rm modules/cluster-core/crds/kustomization.yaml

  # Grafana Alloy
  @curl -L -o modules/cluster-core/crds/grafana-alloy.yaml \
    "https://github.com/grafana/alloy-operator/releases/download/alloy-operator-{{GRAFANA_ALLOY_CRDS_VERSION}}/collectors.grafana.com_alloy.yaml"

  # Sealed Secrets
  @curl -L -o modules/cluster-core/crds/sealed-secrets.yaml \
    "https://github.com/bitnami-labs/sealed-secrets/raw/refs/tags/v{{SEALED_SECRETS_CRDS_VERSION}}/helm/sealed-secrets/crds/bitnami.com_sealedsecrets.yaml"

  # K8UP
  @curl -L -o - \
    "https://github.com/k8up-io/k8up/archive/refs/tags/v{{K8UP_CRDS_VERSION}}.tar.gz" \
    | tar -xz -C modules/cluster-core/crds --strip-components=4 --wildcards "k8up-{{K8UP_CRDS_VERSION}}/charts/k8up/crds/*.yaml"

  # Cert Manager
  @curl -L -o modules/cluster-core/crds/cert-manager.yaml \
    "https://github.com/cert-manager/cert-manager/releases/download/v{{CERT_MANAGER_CRDS_VERSION}}/cert-manager.crds.yaml" \

create: # Like apply, but separated in multiple steps for fresh cluster to work.
  tofu init
  tofu apply -target module.cluster -var-file terraform.secrets.tfvars
  tofu apply -target module.cluster-core.helm_release.talos-ccm -var-file terraform.secrets.tfvars
  tofu apply -target module.cluster-core.helm_release.proxmox-ccm -var-file terraform.secrets.tfvars
  tofu apply -target module.cluster-core.kubectl_manifest.crds -var-file terraform.secrets.tfvars
  tofu apply -target module.cluster-core.helm_release.cilium -var-file terraform.secrets.tfvars
  tofu apply -target module.cluster-core -var-file terraform.secrets.tfvars
  tofu apply -var-file terraform.secrets.tfvars
  @echo '{{CYAN}}{{BOLD}}Cluster provisioned!{{NORMAL}}'
  @echo '{{YELLOW}}{{BOLD}}Add the following output to id.tjo.cloud federation for k8s.tjo.cloud!{{NORMAL}}'
  kubectl get --raw /openid/v1/jwks

apply:
  tofu init
  tofu apply -var-file terraform.secrets.tfvars

apply-only-crds:
  tofu init
  tofu apply -target module.cluster-core.kubectl_manifest.crds -var-file terraform.secrets.tfvars

destroy:
  tofu init
  tofu state rm module.cluster-core || true
  tofu state rm module.cluster-components || true
  tofu destroy -target module.cluster -var-file terraform.secrets.tfvars
  tofu destroy -var-file terraform.secrets.tfvars

remove node: etcd-snapshot
  #!/usr/bin/env bash
  IP=$(tofu output -json | jq -r '.nodes.value | map(select(.name == "{{node}}")) | first | .ipv6')

  talosctl -n ${IP} reset
  kubectl delete node "{{node}}"
  echo "Node {{node}} removed from kubernetes. Now remove it from terraform configuration!"

reboot node:
  #!/usr/bin/env bash
  IP=$(tofu output -json | jq -r '.nodes.value | map(select(.name == "{{node}}")) | first | .ipv6')
  talosctl -n ${IP} reboot

etcd-snapshot prefix="manual":
  #!/usr/bin/env bash
  set -eou pipefail

  NODE=$(tofu output -json | jq -r '.nodes.value | map(select(.type == "controlplane")) | first | .ipv6')

  echo "{{CYAN}}{{BOLD}}== Taking etcd snapshot on node ${NODE}{{NORMAL}}"
  talosctl -n ${NODE} etcd snapshot db.snapshot
  talosctl -n ${NODE} cp /var/lib/etcd/member/snap/db backup/etcd/snapshot-{{prefix}}-$(date +%F_%H-%M-%S)
  echo "{{CYAN}}{{BOLD}}== Snapshoting done!{{NORMAL}}"

etcd-emergancy-restore snapshot:
  #!/usr/bin/env bash
  # TODO: Implement
  talos -n ${ALL_CONTROLPLANE_NODES} reset --graceful=false --reboot --system-labels-to-wipe=EPHEMERAL
  talosctl -n ${SINGLE_CONTROLPLANE_NODE} bootstrap --recover-from={{snapshot}} --recover-skip-hash-check

upgrade-talos version:
  #!/usr/bin/env bash
  set -eou pipefail

  echo "{{CYAN}}{{BOLD}}= Talos version: {{ version }}{{NORMAL}}"

  just etcd-snapshot "pre-upgrade-talos-{{version}}"

  SCHEMATIC_ID=$(tofu output -json | jq -r '.talos.value.schematic_id')
  WORKER_NODES=$(tofu output -json | jq -r '.nodes.value | map(select(.type == "worker")) | sort_by(.name)[] | .ipv6')
  CONTROLPLANE_NODES=$(tofu output -json | jq -r '.nodes.value | map(select(.type == "controlplane")) | sort_by(.name)[] | .ipv6')

  upgrade_node () {
    echo "{{CYAN}}{{BOLD}}=== Upgrading $1{{NORMAL}}"
    talosctl --nodes $1 upgrade --wait \
      --image factory.talos.dev/nocloud-installer/${SCHEMATIC_ID}:{{ version }}
    echo "{{CYAN}}{{BOLD}}==== Upgrade completed.{{NORMAL}}"
  }

  echo "{{CYAN}}{{BOLD}}== Upgrading WORKER nodes{{NORMAL}}"
  for node in ${WORKER_NODES}; do
    upgrade_node $node
  done

  echo "{{RED}}{{BOLD}}== Upgrading CONTROL PLANE nodes{{NORMAL}}"
  for node in ${CONTROLPLANE_NODES}; do
    upgrade_node $node
  done

  just etcd-snapshot "post-upgrade-talos-{{version}}"

upgrade-kubernetes version:
  #!/usr/bin/env bash
  set -eou pipefail

  echo "{{CYAN}}{{BOLD}}= Kubernetes version: {{ version }}{{NORMAL}}"

  just etcd-snapshot "pre-upgrade-kubernetes-{{version}}"

  NODE=$(tofu output -json | jq -r '.nodes.value | map(select(.type == "controlplane")) | first | .ipv6')

  echo "{{CYAN}}{{BOLD}}== Upgrading...{{NORMAL}}"
  talosctl --nodes ${NODE} upgrade-k8s --to {{ version }}
  echo "{{CYAN}}{{BOLD}}== Upgrade completed.{{NORMAL}}"

  just etcd-snapshot "post-upgrade-kubernetes-{{version}}"
