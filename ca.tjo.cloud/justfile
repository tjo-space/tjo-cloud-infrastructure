default:
  @just --list

apply:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu init -upgrade
  tofu apply

destroy:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu  destroy

configure node tag="":
  #!/usr/bin/env sh
  cd {{source_directory()}}/ansible
  export VERSION=$(git describe --tags --always --dirty)
  TAGS=""; if [ -n "{{tag}}" ]; then TAGS="--tags {{tag}}"; fi
  ansible-playbook \
    --extra-vars "@{{source_directory()}}/../global.yaml" \
    --extra-vars "tjo_cloud_version=${VERSION}" \
    --inventory inventory.yaml \
    $TAGS \
    playbook.yaml --limit {{node}}

configure-all tag="":
  #!/usr/bin/env sh
  cd {{source_directory()}}/ansible
  export VERSION=$(git describe --tags --always --dirty)
  TAGS=""; if [ -n "{{tag}}" ]; then TAGS="--tags {{tag}}"; fi
  ansible-playbook \
    --extra-vars "@{{source_directory()}}/../global.yaml" \
    --extra-vars "tjo_cloud_version=${VERSION}" \
    --inventory inventory.yaml \
    $TAGS \
    playbook.yaml

ssh node:
  ssh bine@{{node}}.ca.cloud.internal -p 2222

SOPS_AGE_KEY_FILE := if os() == "linux" {`echo "$HOME/.config/sops/age/keys.txt"`} else { `echo "$HOME/Library/Application Support/sops/age/keys.txt"` }
CERTIFICATES_PATH := source_directory() + "/certificates"
SECRETS_PATH := source_directory() + "/secrets"
TEMPLATES_PATH := source_directory() + "/templates"

create-root-ca:
  #!/usr/bin/env sh
  rm -rf {{CERTIFICATES_PATH}} {{SECRETS_PATH}}
  mkdir -p {{CERTIFICATES_PATH}}/root {{SECRETS_PATH}}/root

  # Will be valid for 25 years.
  step certificate create --no-password --insecure \
    --template {{TEMPLATES_PATH}}/root.tpl \
    --not-after 220000h \
    --kty EC \
    "ca.tjo.cloud - Root" \
    {{CERTIFICATES_PATH}}/root/ca.crt \
    {{SECRETS_PATH}}/root/ca.key

  just encrypt-secrets root

  step certificate fingerprint {{CERTIFICATES_PATH}}/root/ca.crt > {{CERTIFICATES_PATH}}/root/ca.fingerprint

  cp {{CERTIFICATES_PATH}}/root/ca.crt {{source_directory()}}/ansible/root/var/www/root.crt
  cp {{CERTIFICATES_PATH}}/root/ca.fingerprint {{source_directory()}}/ansible/root/var/www/root.fingerprint

create-root-intermediate-ca:
  #!/usr/bin/env sh
  just decrypt-secrets root
  mkdir -p {{CERTIFICATES_PATH}}/intermediate {{SECRETS_PATH}}/intermediate

  YEAR=$(date -u +%Y)

  # Will be valid for 1 year.
  # Allows to have two more Intermediates under.
  step certificate create --no-password --insecure \
    --template {{TEMPLATES_PATH}}/intermediate.tpl \
    --ca {{CERTIFICATES_PATH}}/root/ca.crt \
    --ca-key {{SECRETS_PATH}}/root/ca.key \
    --not-after 9000h \
    --kty EC \
    --set "maxPathLen=3" \
    "ca.tjo.cloud - ${YEAR} Intermediate" \
    {{CERTIFICATES_PATH}}/intermediate/${YEAR}.crt \
    {{SECRETS_PATH}}/intermediate/${YEAR}.key

  just encrypt-secrets intermediate

  step certificate fingerprint {{CERTIFICATES_PATH}}/intermediate/${YEAR}.crt > {{CERTIFICATES_PATH}}/intermediate/${YEAR}.fingerprint

sign-issuing-intermediate-ca node:
  #!/usr/bin/env sh
  just decrypt-secrets intermediate

  YEAR=$(date -u +%Y)
  CSR=$(mktemp)
  CRT=$(mktemp)

  ssh bine@{{node}}.ca.cloud.internal -p 2222 "sudo cat /etc/caddy/root.csr" > $CSR

  # Will be valid for 400 days.
  # We allow it to sign another intermediary.
  # We upload it
  step certificate sign \
    --template {{TEMPLATES_PATH}}/intermediate.tpl \
    --not-after 9600h \
    --set "maxPathLen=2" \
    $CSR \
    {{CERTIFICATES_PATH}}/intermediate/${YEAR}.crt \
    {{SECRETS_PATH}}/intermediate/${YEAR}.key > $CRT

  just remove-secrets

  # Upload Certificate
  cat $CRT | ssh bine@{{node}}.ca.cloud.internal -p 2222 "sudo tee /etc/caddy/root.crt"

  # Change ownership
  ssh bine@{{node}}.ca.cloud.internal -p 2222 "sudo chown caddy:caddy /etc/caddy/root.crt && sudo chmod 0400 /etc/caddy/root.crt"

remove-secrets:
  #!/usr/bin/env sh
  rm -rf {{SECRETS_PATH}}

encrypt-secrets type:
  #!/usr/bin/env sh
  tar -vzcf - -C {{source_directory()}} secrets/{{type}} \
    | age --encrypt -R {{source_directory()}}/../age.keys > {{source_directory()}}/secrets.{{type}}.encrypted
  just remove-secrets

decrypt-secrets type:
  #!/usr/bin/env sh
  just remove-secrets {{type}}

  mkdir -p {{SECRETS_PATH}}/{{type}}
  chmod 700 {{SECRETS_PATH}}
  cat {{source_directory()}}/secrets.{{type}}.encrypted \
    | age --decrypt -i "{{SOPS_AGE_KEY_FILE}}" | tar -vzxf - -C {{source_directory()}}
