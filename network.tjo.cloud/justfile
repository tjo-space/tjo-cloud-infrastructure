default:
  @just --list

download:
  #!/usr/bin/env sh
  set -euo pipefail
  mkdir -p {{source_directory()}}/iso

  curl -L -o - https://downloads.openwrt.org/releases/23.05.5/targets/x86/64/openwrt-23.05.5-x86-64-generic-ext4-combined-efi.img.gz \
    | gunzip > {{source_directory()}}/iso/openwrt-23.05.5-x86-64-generic-ext4-combined-efi.img

apply:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu init
  tofu apply

destroy:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu destroy

destroy-only node:
  #!/usr/bin/env sh
  cd {{source_directory()}}/terraform
  tofu destroy -target='proxmox_virtual_environment_vm.nodes["{{node}}"]'

configure-all:
  #!/usr/bin/env sh
  set -euo pipefail
  for node in $(cat config.yaml | yq -r '.nodes | keys | .[]')
  do
    just configure ${node}
  done

configure node:
  #!/usr/bin/env sh
  set -euo pipefail

  export NODE="{{node}}"
  export TMPDIR="$(mktemp -d)"
  export CONFIG="{{source_directory()}}/config.yaml"

  cd {{source_directory()}}/root

  echo "Configuring ${NODE}.network.tjo.cloud"

  echo "- Generating configuration ${TMPDIR}..."
  for file in $(find * -type f)
  do
    echo " - $file"
    mkdir -p ${TMPDIR}/$(dirname $file)
    gomplate --file $file --datasource config=${CONFIG} > ${TMPDIR}/${file}
  done

  echo "- Copying configuration to node..."
  pushd ${TMPDIR}
  for file in $(find * -type f)
  do
    echo "- $file"
    cat $file | tailscale ssh root@${NODE}-network-tjo-cloud "cat > /$file"
  done
  popd

  echo "- Configuring tailscale"
  tailscale ssh root@${NODE}-network-tjo-cloud <<'EOL'
  tailscale up \
    --advertise-routes=10.0.0.0/10,fd74:6a6f::/32 \
    --snat-subnet-routes=true \
    --accept-dns=false \
    --ssh \
    --reset
  EOL

  echo "- Installing zerotier"
  tailscale ssh root@${NODE}-network-tjo-cloud "opkg update"
  tailscale ssh root@${NODE}-network-tjo-cloud "opkg install zerotier"

  echo "- Configuring zerotier"
  tailscale ssh root@${NODE}-network-tjo-cloud <<'EOL'
  uci set zerotier.global.enabled='1'
  uci set zerotier.global.local_conf_path=/etc/zerotier.conf
  uci delete zerotier.earth
  uci delete zerotier.mynet
  uci set zerotier.tjo_cloud=network
  uci set zerotier.tjo_cloud.id=b6079f73c6379990
  uci commit zerotier
  EOL

  echo "- Installing Bird"
  tailscale ssh root@${NODE}-network-tjo-cloud "opkg update"
  tailscale ssh root@${NODE}-network-tjo-cloud "opkg install bird2 bird2c"
  tailscale ssh root@${NODE}-network-tjo-cloud "service bird reload"

  echo "- Reboot router in 5 seconds..."
  sleep 5
  echo "- Rebooting router..."
  tailscale ssh root@${NODE}-network-tjo-cloud "reboot"
